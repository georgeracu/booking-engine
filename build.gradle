group 'com.georgeracu'
version '1.0-SNAPSHOT'

buildscript {
    ext.kotlin_version = '1.6.10'
    ext.dropwizard_version = '2.0.28'
    ext.config_file = 'config.yml'
    ext.jupiter_version = '5.8.2'
    ext.mockk_version = '1.12.2'

    repositories {
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.kotlin:kotlin-noarg:$kotlin_version"
        classpath "gradle.plugin.com.github.johnrengelman:shadow:7.1.2"
    }
}

apply plugin: "com.github.johnrengelman.shadow"
apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: 'application'

sourceCompatibility = 16
targetCompatibility = 16

mainClassName = 'com.georgeracu.booking.MainApp'

repositories {
    mavenCentral()
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-common:$kotlin_version"
    implementation "io.dropwizard:dropwizard-core:$dropwizard_version"
    implementation "io.dropwizard:dropwizard-client:$dropwizard_version"
    implementation "io.dropwizard:dropwizard-testing:$dropwizard_version"

    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"


    testImplementation "org.junit.jupiter:junit-jupiter-api:$jupiter_version"
    testImplementation "org.junit.jupiter:junit-jupiter-engine:$jupiter_version"
    testImplementation "io.mockk:mockk:$mockk_version"
    testImplementation "org.assertj:assertj-core:3.22.0"

    testImplementation "io.dropwizard:dropwizard-testing:$dropwizard_version"
    testImplementation "io.dropwizard:dropwizard-client:$dropwizard_version"
}

// Configure the shadow jar task

shadowJar {
    mergeServiceFiles()
    version = project.version
    manifest {
        attributes 'Main-Class': mainClassName
    }
}

// Start the Dropwizard service
run {
    args 'server', config_file
}

compileKotlin {
    kotlinOptions.jvmTarget = "16"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "16"
}

test {
    testLogging {
        exceptionFormat = 'full'
        events 'FAILED', 'SKIPPED', 'PASSED'
        showStandardStreams = true
        afterSuite { desc, result ->
            if (!desc.parent) {
                println "\nTest result: ${result.resultType}"
                println "Test summary: ${result.testCount} tests, " +
                        "${result.successfulTestCount} succeeded, " +
                        "${result.failedTestCount} failed, " +
                        "${result.skippedTestCount} skipped"
            }
        }
    }

    useJUnitPlatform {}
}

